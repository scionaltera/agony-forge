plugins {
    id 'info.solidsoft.pitest' version '1.7.4'
    id 'org.owasp.dependencycheck' version '7.1.1'
    id 'com.palantir.docker' version '0.33.0'
    id 'org.springframework.boot' version '2.3.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
}

group 'com.agonyforge'
version '0.5.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations.maybeCreate("pitest")

dependencies {
    pitest group: 'org.pitest', name: 'pitest-junit5-plugin', version: '0.16'

    implementation 'javax.inject:javax.inject:1'
    implementation 'javax.interceptor:javax.interceptor-api:1.2.2'

    implementation 'mysql:mysql-connector-java:8.0.29'
    implementation 'org.flywaydb:flyway-core:5.2.4'

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-freemarker'
    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    implementation('org.springframework.boot:spring-boot-starter-websocket') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    implementation 'org.springframework.boot:spring-boot-starter-jetty'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    implementation 'org.springframework.session:spring-session-jdbc'

    implementation 'io.projectreactor.netty:reactor-netty:0.9.7.RELEASE'

    implementation 'org.webjars:webjars-locator-core:0.45'
    implementation 'org.webjars:jquery:3.5.1'
    implementation 'org.webjars:popper.js:2.0.2'
    implementation 'org.webjars:bootstrap:4.5.0'
    implementation 'org.webjars:font-awesome:5.13.0'

    implementation 'commons-io:commons-io:2.7'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'org.mockito:mockito-core:3.3.0'
}

test {
    useJUnitPlatform()
}

jar {
    enabled = false // don't build the plain, non-executable jar
}

bootJar {
    manifest {
        attributes(
            'Implementation-Title': jar.getArchiveBaseName().get(),
            'Implementation-Version': getArchiveVersion())
    }
}

docker {
    def applicationName = jar.getArchiveBaseName().get()
    name "scionaltera/${applicationName}"
    dockerfile file('src/main/docker/local/Dockerfile')
    files tasks.bootJar.outputs
}

project.tasks.build.dependsOn tasks.docker

pitest {
    excludedClasses = [
        'com.agonyforge.core.AgonyForge',
        'com.agonyforge.core.config.*'
    ]
    excludedMethods = [
        'hashCode',
        'equals',
        'toString'
    ]
    coverageThreshold = 70
    mutationThreshold = 70
    timestampedReports = false
}

dependencyCheck {
    suppressionFile "codequality/cve-suppressions.xml"
}

check.dependsOn project.tasks.pitest, project.tasks.dependencyCheckAnalyze

// ensure pitest runs after junit, because pitest requires all tests to have passed
project.tasks.pitest.mustRunAfter test
