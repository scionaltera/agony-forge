group 'com.agonyforge'
version '0.5.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'info.solidsoft.pitest'
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'org.kordamp.gradle.stats'
apply plugin: 'docker'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = 1.8
targetCompatibility = 1.8

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    configurations.maybeCreate("pitest")

    dependencies {
        classpath group: 'org.owasp', name: 'dependency-check-gradle', version: '5.2.4'
        classpath group: 'org.kordamp.gradle', name: 'stats-gradle-plugin', version: '0.2.2'
        classpath group: 'se.transmode.gradle', name: 'gradle-docker', version: '1.2'
        classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: '2.2.3.RELEASE'
        classpath group: 'info.solidsoft.gradle.pitest', name: 'gradle-pitest-plugin', version: '1.4.0'

        pitest group: 'org.pitest', name: 'pitest-junit5-plugin', version: '0.11'
    }
}

repositories {
    mavenCentral()
}

configurations.compile {
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
}

dependencies {
    compile group: 'javax.inject', name: 'javax.inject', version: '1'
    compile group: 'javax.interceptor', name: 'javax.interceptor-api', version: '1.2.2'

    compile group: 'mysql', name: 'mysql-connector-java', version: '8.0.17'
    compile group: 'org.flywaydb', name: 'flyway-core', version: '5.2.4'

    compile group: 'org.springframework.boot', name: 'spring-boot-starter'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-freemarker'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-websocket'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jetty'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security'

    compile group: 'org.springframework.session', name: 'spring-session-jdbc'

    // required for using ActiveMQ as a message broker
    compile group: 'io.projectreactor.netty', name: 'reactor-netty', version: '0.9.3.RELEASE'

    compile group: 'org.webjars', name: 'webjars-locator-core', version: '0.43'
    compile group: 'org.webjars', name: 'jquery', version: '3.4.1'
    compile group: 'org.webjars', name: 'popper.js', version: '1.15.0'
    compile group: 'org.webjars', name: 'bootstrap', version: '4.3.1'
    compile group: 'org.webjars', name: 'font-awesome', version: '5.12.0'

    compile group: 'commons-io', name: 'commons-io', version: '2.6'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.5.1'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.2.4'
}

test {
    useJUnitPlatform()
}

task buildDocker(type: Docker) {
    push = false
    applicationName = jar.getArchiveBaseName().get()
    dockerfile = file('src/main/docker/local/Dockerfile')
    tagVersion = "latest"
    tag = "scionaltera/${applicationName}"

    doFirst {
        copy {
            from jar
            into stageDir
        }
    }
}

build.dependsOn project.tasks.buildDocker

pitest {
    testPlugin = "junit5"
    excludedClasses = [
        'com.agonyforge.core.AgonyForge',
        'com.agonyforge.core.config.*'
    ]
    excludedMethods = [
        'hashCode',
        'equals'
    ]
    coverageThreshold = 70
    mutationThreshold = 70
    timestampedReports = false
}

dependencyCheck {
    suppressionFile "codequality/cve-suppressions.xml"
}

check.dependsOn project.tasks.pitest, project.tasks.dependencyCheckAnalyze, project.tasks.stats

// ensure pitest runs after junit, because pitest requires all tests to have passed
project.tasks.pitest.mustRunAfter test
